_SECTION_BEGIN("SuperTrend with ADX filter Strategy");

function SuperTrend(atr_period, factor)
{
    Up            = (H+L)/2 + (factor*ATR(atr_period));
    Down          = (H+L)/2 - (factor*ATR(atr_period));
    iATR          = ATR(atr_period);
    TrendUp       = Null;
    TrendDown     = Null;
    trend[0]      = 1;
    changeOfTrend = 0;
    flag          = 0;
    flagh         = 0;

    for (i = 1; i <BarCount-1; i++){
        TrendUp[i]   = Null;
        TrendDown[i] = Null;
        Trend[i]     = 1;
  
        if (Close[i]>Up[i-1]){
            Trend[i] = 1;
            if (Trend[i-1] == -1){
                changeOfTrend = 1;
            }
        }else if (Close[i]<Down[i-1]){
            Trend[i] = -1;
            if (Trend[i-1] == 1){
                changeOfTrend = 1;
            }
        }else{
          Trend[i] = Trend[i-1];
          changeOfTrend = 0;
        }

        if (Trend[i]<0 && Trend[i-1]>0) {
            flag = 1;
        }else {
            flag = 0;
        }
        
        if (Trend[i]>0 && Trend[i-1]<0) {
            flagh = 1;
        }
        else {
            flagh = 0;
        }
        
        if (Trend[i]>0 && Down[i]<Down[i-1]){
            Down[i] = Down[i-1];
        }
        if (Trend[i]<0 && Up[i]>Up[i-1]){
            Up[i] = Up[i-1];
        }
        
        if (flag==1){
            Up[i] = (H[i]+L[i])/2+(Factor*iATR[i]);;
        } 
        if (flagh==1){
            Down[i] =(H[i]+L[i])/2-(Factor*iATR[i]);;
        }
        if (Trend[i]==1){
            TrendUp[i] = Down[i];
            if (changeOfTrend == 1) {
                TrendUp[i-1] = TrendDown[i-1];
                changeOfTrend = 0;
            }
        }else if (Trend[i]==-1){
            TrendDown[i] = Up[i];
            if (changeOfTrend == 1) {
                TrendDown[i-1] = TrendUp[i-1];
                changeOfTrend = 0;
            }
        }
    }

    VarSet("supertrend_up", TrendUp);
    VarSet("supertrend_down", TrendDown);
    VarSet("supertrend_trend", Trend);
}


// Parameters
ATR_period = optimize("ATRPeriod", Param("ATRPeriod", 7, 1, 30, 1) , 1, 30, 1);
factor     = optimize("Fator", Param("Factor", 3, 1, 25, 1), 1, 25, 1);
ADX_limit  = optimize("ADXLimit", Param("ADXLimit", 20, 0, 100), 10, 30, 5);
ADX_tfs    = ParamList("ADXTimeFrame", "1h|1D|1W");
ADX_period = optimize("ADXPeriod", Param("ADXPeriod", 14, 1, 30), 10, 30, 5);

slippage   = Param("Slippage", 1, 0, 10, 0.1);
fut_mode   = ParamToggle("Futures Mode", "No|Yes", 1);
ADX_tf     = -50;

if (fut_mode){
    // Initial parameters
    SetOption("FuturesMode" ,True);
    SetPositionSize(1, spsShares);
}

if (ADX_tfs == "1h")
{
    ADX_tf   = inHourly;
}
else if(ADX_tfs == "1D")
{
    ADX_tf   = inDaily;
}
else if (ADX_tfs == "1W")
{
    ADX_tf   = inWeekly;
}
else
{
    ADX_tf   = -50;
}

function ADX_TF_FUN(ADX_period_, ADX_tf_)
{
    TimeFrameSet(ADX_tf_);
    _adx_ind = ADX(ADX_period_);
    TimeFrameRestore();
    return TimeFrameExpand(_adx_ind, ADX_tf_);
}

ADX_ind = Null;
if (ADX_tf != -50)
{ 
    ADX_ind = ADX_TF_FUN(ADX_period, ADX_tf);
}
else
{
    ADX_ind = ADX(ADX_period);
}

// Call Supertrend
SuperTrend(ATR_period, factor);

// Get inds
TrendUp   = VarGet("supertrend_up");
TrendDown = VarGet("supertrend_down");
Trend     = VarGet("supertrend_trend");

// Buy Sell rules
Buy     = (ADX_ind > ADX_limit) AND (Trend == 1);
Sell    = (Trend == -1);
Buy     = ExRem(Buy,Sell);
Sell    = ExRem(Sell,Buy);
Short   = (ADX_ind > ADX_limit) AND (Trend == -1);
Cover   = Trend == 1;
Short   = ExRem(Short, Cover);
Cover   = ExRem(Cover, Short);

// slippages
BuyPrice += slippage;
SellPrice -= slippage;
ShortPrice -= slippage;
CoverPrice += slippage;

Plot(TrendUp, "Trend", colorGreen);
Plot(TrendDown, "Down", colorRed);
Plot(Close, "Price", colorWhite, styleCandle);

_SECTION_END();
